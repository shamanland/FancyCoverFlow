buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.github.dcendents:android-maven-plugin:1.1'
    }
}

def getFullVersionName() {
    def result = VERSION_NAME

    if (hasProperty('VERSION_NAME_SUFFIX')) {
        result += "." + getProperty('VERSION_NAME_SUFFIX')
    }

    if (Boolean.valueOf(getProperty("SNAPSHOT").toString())) {
        result += "-SNAPSHOT"
    }

    return result
}

subprojects {
    group GROUP
    version getFullVersionName()

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'android-sdk-manager'

    if (name.startsWith('lib')) {
        apply plugin: 'com.android.library'
        apply plugin: 'android-maven'
    } else {
        apply plugin: 'com.android.application'
    }

    android {
        compileSdkVersion COMPILE_SDK_VERSION as int
        buildToolsVersion BUILD_TOOLS_VERSION

        defaultConfig {
            minSdkVersion MIN_SDK_VERSION as int
            targetSdkVersion TARGET_SDK_VERSION as int
            versionCode VERSION_CODE as int
            versionName getFullVersionName()

            // workaround for https://code.google.com/p/android/issues/detail?id=52962
            String snapshot = (SNAPSHOT as boolean) ? "Boolean.parseBoolean(\"true\")" : "false"
            buildConfigField 'boolean', 'SNAPSHOT', snapshot
        }

        lintOptions {
            abortOnError false
        }
    }
}
